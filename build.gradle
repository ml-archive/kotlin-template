// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versions = [
            kotlin             : '1.3.41',
            coroutines         : '1.3.0-M2',
            constraint_layout  : '2.0.0-beta2',
            dagger             : "2.24",
            lifecycle          : "2.1.0-rc01",
            hockey_sdk         : "5.2.0",
            timber             : "4.7.1",
            junit              : '4.12',
            espresso           : '3.2.0',
            mockito            : '2.28.2',
            nodes_nstack_kotlin: '3.0.5',
            nstack_plugin      : '3.0.3',
            nodes_arch         : '2.3.5',
            retrofit           : "2.6.0",
            okhttp             : "3.12.3",
            rx_android         : '2.1.1',
            rx_java            : "2.2.10",
            rx_kotlin          : "2.3.0",
            ktx                : "1.0.2",
            rx_lint            : '1.7.0',
            material           : '1.1.0-alpha09',
            archCore           : '2.0.1',
            navigation         : '2.0.0'
    ]

    ext.keys = [
            appId       : 'bOdrNuZd4syxuAz6gyCb3xwBCjA8U4h4IcQI',
            apiKey      : 'X0ENl5QpKI51tS9CzKSt1PGwfZeq2gBMTU58',
            acceptHeader: "da-DK"
    ]

    ext.sdks = [
            compileSdkVersion: 28,
            buildToolsVersion: '28.0.3',
            minSdkVersion    : 21,
            targetSdkVersion : 28
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "dk.nodes.nstack:translation:${versions.nstack_plugin}"
        classpath "com.github.gfx.ribbonizer:ribbonizer-plugin:2.1.0"
        classpath "dk.nodes.ci:bitrise:1.0"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.23.1"
    id 'com.github.ben-manes.versions' version "0.21.0"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://maven.google.com' }
        jcenter()
        mavenLocal()
    }
}


subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint('0.33.0')
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}