// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versions = [
            kotlin             : '1.3.31',
            coroutines         : '1.2.1',
            constraint_layout  : '2.0.0-beta1',
            lifecycle          : "2.1.0-alpha04",
            hockey_sdk         : "5.2.0",
            timber             : "4.7.1",
            junit              : '4.12',
            espresso           : '3.2.0',
            mockito            : '2.28.2',
            nodes_nstack_kotlin: '2.3.0',
            nodes_arch         : '2.3.3',
            retrofit           : "2.6.0",
            okhttp             : "3.12.1",
            rx_android         : '2.1.1',
            rx_java            : "2.2.9",
            rx_kolin           : "2.3.0",
            ktx                : "1.0.2",
            rx_lint            : '1.7.0',
            material           : '1.1.0-alpha07',
            koin               : '2.0.1'
    ]

    ext.keys = [
            appId       : 'BmZHmoKuU99A5ZnOByOiRxMVSmAWC2yBz3OW',
            apiKey      : 'yw9go00oCWt6zPhfbdjRYXiHRWmkQZQSuRke',
            acceptHeader: "da-DK"
    ]

    ext.sdks = [
            compileSdkVersion: 28,
            buildToolsVersion: '28.0.3',
            minSdkVersion    : 19,
            targetSdkVersion : 28
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "dk.nodes.nstack:translation:2.3.0"
        classpath "com.github.gfx.ribbonizer:ribbonizer-plugin:2.1.0"
        classpath "dk.nodes.ci:bitrise:1.0"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.23.0"
    id 'com.github.ben-manes.versions' version "0.21.0"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://maven.google.com' }
        jcenter()
        mavenLocal()
    }
}


subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint('0.33.0')
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}